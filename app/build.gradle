buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath "io.realm:realm-gradle-plugin:3.5.0"
        classpath 'io.fabric.tools:gradle:1.23.0'
        classpath "io.mironov.smuggler:smuggler-plugin:0.12.14"
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "io.mironov.smuggler"
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

def versionMajor = 0
def versionMinor = 0
def versionPatch = 1

def buildTime = new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    defaultConfig {
        // FIXME Replace with your app's package name
        applicationId "com.kaciula.archiman"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        // FIXME: Create your own keystore with secret keys and passwords
        release {
            storeFile file("key/archiman.jks")
            storePassword 'Archiman987'
            keyAlias 'archiman.key'
            keyPassword 'Archiman987'
        }
    }

    // FIXME Replace with your app's package name followed by fileprovider
    final FILE_PROVIDER_DEBUG = "com.kaciula.archiman.fileprovider"
    final FILE_PROVIDER_AUTHORITY = "FILE_PROVIDER_AUTHORITY"

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev-" + buildTime
            testCoverageEnabled false

            buildConfigField "String", FILE_PROVIDER_AUTHORITY, "\"" + FILE_PROVIDER_DEBUG + "\""
            manifestPlaceholders = [fileProviderAuthority: FILE_PROVIDER_DEBUG]
        }
    }
    flavorDimensions "one"
    productFlavors {
        prod {
            dimension "one"
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            // FIXME Replace with your app's name
            outputFileName = "Archiman-${variant.versionName}.apk"
        }
    }
}

dependencies {
    final SUPPORT_LIBRARY_VERSION = '25.3.1'
    final DAGGER_VERSION = '2.11'
    final LEAK_CANARY_VERSION = '1.5.1'
    final BUTTER_KNIFE_VERSION = '8.7.0'
    final RETROFIT_VERSION = '2.3.0'
    final OKHTTP_VERSION = '3.8.1'
    final CONDUCTOR_VERSION = '2.1.4'
    final DEBUG_DRAWER = '0.7.0'
    final RUNNER_VERSION = '0.5'
    final JODA_TIME_ANDROID_VERSION = '2.9.9'
    final JODA_TIME_VERSION = '2.9.9'
    final STETHO_VERSION = '1.5.0'

    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':domain')

    compile "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    compile "com.android.support.constraint:constraint-layout:1.0.2"

    compile 'com.jakewharton.timber:timber:4.5.1'
    compile "com.jakewharton:butterknife:$BUTTER_KNIFE_VERSION"
    kapt "com.jakewharton:butterknife-compiler:$BUTTER_KNIFE_VERSION"

    compile "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:converter-moshi:$RETROFIT_VERSION"
    compile "com.squareup.retrofit2:adapter-rxjava2:$RETROFIT_VERSION"

    compile "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"

    compile "com.google.dagger:dagger:$DAGGER_VERSION"
    provided 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2
    kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    compile "io.reactivex.rxjava2:rxjava:$RXJAVA_VERSION"
    compile "io.reactivex.rxjava2:rxandroid:2.0.1"
    compile "com.jakewharton.rxrelay2:rxrelay:2.0.0"

    compile "com.bluelinelabs:conductor:$CONDUCTOR_VERSION"
    compile "com.bluelinelabs:conductor-support:$CONDUCTOR_VERSION"
    compile "com.bluelinelabs:conductor-rxlifecycle2:$CONDUCTOR_VERSION"

    compile('com.crashlytics.sdk.android:crashlytics:2.6.8@aar') {
        transitive = true;
    }
    compile 'com.squareup.moshi:moshi:1.5.0'
    compile "net.danlew:android.joda:$JODA_TIME_ANDROID_VERSION"
    compile 'com.dmitrymalkovich.android:material-design-dimens:1.4'

    compile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$KOTLIN_VERSION"

    // Debug only dependencies
    debugCompile "com.squareup.leakcanary:leakcanary-android:$LEAK_CANARY_VERSION"
    debugCompile 'com.squareup.haha:haha:2.0.3'

    debugCompile 'com.amitshekhar.android:debug-db:1.0.1'
    debugCompile "io.palaima.debugdrawer:debugdrawer:$DEBUG_DRAWER"
    debugCompile "io.palaima.debugdrawer:debugdrawer-commons:$DEBUG_DRAWER"
    debugCompile "io.palaima.debugdrawer:debugdrawer-actions:$DEBUG_DRAWER"
    debugCompile 'jp.wasabeef:takt:1.0.5'
    debugCompile "com.squareup.okhttp3:logging-interceptor:$OKHTTP_VERSION"
    debugCompile 'com.github.simonpercic:oklog3:2.1.0'

    debugCompile "com.facebook.stetho:stetho:$STETHO_VERSION"
    debugCompile("com.facebook.stetho:stetho-okhttp3:$STETHO_VERSION") {
        exclude group: 'com.squareup.okhttp3'
    }
    debugCompile("com.facebook.stetho:stetho-timber:$STETHO_VERSION") {
        exclude group: 'com.jakewharton.timber'
    }

    // Unit testing dependencies.
    testCompile project(':domain')
    testCompile "junit:junit:$JUNIT_VERSION"
    testCompile "org.mockito:mockito-all:$MOCKITO_VERSION"
    testCompile "org.hamcrest:hamcrest-all:$HAMCREST_VERSION"
    testCompile "joda-time:joda-time:$JODA_TIME_VERSION"

    // Force usage of support annotations in the test app, since it is internally used by the runner module.
    androidTestCompile "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    androidTestCompile "com.android.support.test:runner:$RUNNER_VERSION"
    androidTestCompile "com.android.support.test:rules:$RUNNER_VERSION"
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'tools.fastlane:screengrab:1.1.0'
    androidTestCompile "com.google.code.findbugs:jsr305:3.0.1"
}